plugins {
    id 'java'
    id "org.sonarqube" version "3.1.1"
    id 'jacoco'


    id "nebula.lint" version "16.9.0"
    id 'nebula.maven-publish' version '17.0.0'
    id 'nebula.javadoc-jar' version '17.0.0'
    id 'nebula.source-jar' version '17.0.0'


    id 'nebula.info' version '5.1.0'
    id 'nebula.contacts' version '5.1.0'

    id 'signing'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

sourceCompatibility = '11'


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.2")
}


sonarqube {
    properties {
        property "sonar.projectKey", "jijins1_spring-data-query-param"
        property "sonar.organization", "jijins1"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    apply plugin: 'nebula.maven-publish'
    apply plugin: 'nebula.javadoc-jar'
    apply plugin: 'nebula.source-jar'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.contacts'
    apply plugin: "nebula.lint"

    apply plugin: 'signing'
    apply plugin: 'io.spring.dependency-management'

    contacts {
        'leo.gerberon@live.fr' {
            github 'jijins1'
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:1.0.1.RELEASE'
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }


    dependencies {

        testImplementation "org.assertj:assertj-core:3.11.1"
        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        testImplementation 'org.jeasy:easy-random-core:5.0.0'
        testImplementation 'io.github.glytching:junit-extensions:2.4.0'
        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }


    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.enabled true
            csv.enabled false
        }

    }


    publishing {
        repositories {
            maven {
                if (project.hasProperty("release")) {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                } else {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                }
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }

        }

        publications {
            nebula(MavenPublication) {
                pom {
                    if (project.hasProperty("release")) {
                        version = project.version;
                    } else {
                        version = project.version + '-SNAPSHOT'
                    }
                    name = 'Spring Data Query Param'
                    description = 'Generate easily usable criteria for data jpa'
                    url = 'https://github.com/jijins1/spring-data-query-param'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }

        signing {
            def signingKey = findProperty("signingKey").replace("\\n", "\n")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.nebula
        }

    }


}


jacoco {
    toolVersion = "0.8.6"
}







